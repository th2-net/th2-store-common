plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "com.jfrog.bintray" version '1.8.5'
}

group = 'com.exactpro.th2'
version = release_version

    ext {
        sharedDir                   = file("${project.rootDir}/shared")
        vertxVersion                = '3.8.5'


    }

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/th2'
        }

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:2.9.1')
    implementation 'com.exactpro.th2:common:2.6.0'
    implementation 'com.exactpro.th2:grpc-common:2.3.2'

    implementation 'com.google.protobuf:protobuf-java-util'

    implementation "org.jetbrains:annotations:20.0.0"

    implementation "io.vertx:vertx-rx-java2:$vertxVersion"
}

sourceCompatibility = 11
targetCompatibility = 11

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
        bintrayPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId rootProject.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', rootProject.name)
                root.appendNode('url', project.vcs_url)
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}

bintray {
    user = "${bintray_user}"
    key = "${bintray_key}"
    publications = ['bintrayPublication']
    pkg {
        repo = 'th2'
        name = rootProject.name
        userOrg = 'exactpro'
        licenses = ['Apache-2.0']
        vcsUrl = project.vcs_url
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
    publish = true
}